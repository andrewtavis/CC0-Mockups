# tool: scribe-data
# All Danish (Q9035) adjectives and some of the available forms.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?pluralPositive
  ?pluralSuperlative
  ?comparative

WHERE {
  ?lexeme dct:language wd:Q9035 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .

  # MARK: Definite

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularDefiniteForm .
    ?singularDefiniteForm ontolex:representation ?singularDefinite ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q53997851 ;
      wikibase:grammaticalFeature wd:Q3482678 .
  } .

  # MARK: Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralPositiveForm .
    ?pluralPositiveForm ontolex:representation ?pluralPositive ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q3482678 .
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralSuperlativeForm .
    ?pluralSuperlativeForm ontolex:representation ?pluralSuperlative ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q1817208 .
  } .

  # MARK: Comparative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comparativeForm .
    ?comparativeForm ontolex:representation ?comparative ;
      wikibase:grammaticalFeature wd:Q14169499 .
  } .
}
