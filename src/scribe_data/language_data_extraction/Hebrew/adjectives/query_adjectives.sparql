# tool: scribe-data
# All Hebrew (Q9288) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?femSingular
  ?femSingularConstruct
  ?femPlural
  ?femPluralConstruct
  ?masSingular
  ?masSingularConstruct
  ?masPlural
  ?masPluralConstruct

WHERE {
  ?lexeme dct:language wd:Q9288 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .
    FILTER(lang(?adjective) = "he")

  # MARK: Feminine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femSingularForm .
    ?femSingularForm ontolex:representation ?femSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786 .
    FILTER NOT EXISTS {
      ?femSingularForm wikibase:grammaticalFeature wd:Q1641446 .
    }
    FILTER(lang(?femSingular) = "he") .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femSingularConstructForm .
    ?femSingularConstructForm ontolex:representation ?femSingularConstruct ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786, wd:Q1641446 .
      FILTER(lang(?femSingularConstruct) = "he") .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femPluralForm .
    ?femPluralForm ontolex:representation ?femPlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786 .
    FILTER NOT EXISTS {
      ?femPluralForm wikibase:grammaticalFeature wd:Q1641446 .
    }
    FILTER(lang(?femPlural) = "he") .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femPluralConstructForm .
    ?femPluralConstructForm ontolex:representation ?femPluralConstruct ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786, wd:Q1641446 .
    FILTER(lang(?femPluralConstruct) = "he") .
  }

  # MARK: Masculine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masSingularForm .
    ?masSingularForm ontolex:representation ?masSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
    FILTER NOT EXISTS {
      ?masSingularForm wikibase:grammaticalFeature wd:Q1641446 .
    }
    FILTER(lang(?masSingular) = "he") .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masSingularConstructForm .
    ?masSingularConstructForm ontolex:representation ?masSingularConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786, wd:Q1641446 .
    FILTER(lang(?masSingularConstruct) = "he") .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masPluralForm .
    ?masPluralForm ontolex:representation ?masPlural ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786 .
    FILTER NOT EXISTS {
      ?masPluralForm wikibase:grammaticalFeature wd:Q1641446 .
    }
    FILTER(lang(?masPlural) = "he") .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masPluralConstructForm .
    ?masPluralConstructForm ontolex:representation ?masPluralConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786, wd:Q1641446 .
      FILTER(lang(?masPluralConstruct) = "he") .
  }
}
