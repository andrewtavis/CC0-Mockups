# tool: scribe-data
# All French (Q150) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?presFPS ?presSPS ?presTPS
  ?presFPP ?presSPP ?presTPP
  ?pretFPS ?pretSPS ?pretTPS
  ?pretFPP ?pretSPP ?pretTPP

WHERE {
  ?lexeme dct:language wd:Q150 ;
    wikibase:lexicalCategory wd:Q24905 .

  # MARK: Infinitive

  ?lexeme ontolex:lexicalForm ?infinitiveForm .
  ?infinitiveForm ontolex:representation ?infinitive ;
    wikibase:grammaticalFeature wd:Q179230 ;

  # MARK: Indicative Present

  # FPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presFPSForm .
    ?presFPSForm ontolex:representation ?presFPS ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q110786, wd:Q682111, wd:Q192613 .
  }

  # SPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSPSForm .
    ?presSPSForm ontolex:representation ?presSPS ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786, wd:Q682111, wd:Q192613 .
  }

  # TPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presTPSForm .
    ?presTPSForm ontolex:representation ?presTPS ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q110786, wd:Q682111, wd:Q192613 .
  }

  # FPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presFPPForm .
    ?presFPPForm ontolex:representation ?presFPP ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786, wd:Q682111, wd:Q192613 .
  }

  # SPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSPPForm .
    ?presSPPForm ontolex:representation ?presSPP ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786, wd:Q682111, wd:Q192613 .
  }

  # TPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presTPPForm .
    ?presTPPForm ontolex:representation ?presTPP ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q146786, wd:Q682111, wd:Q192613 .
  }

  # MARK: Indicative Preterite

  # FPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretFPSForm .
    ?pretFPSForm ontolex:representation ?pretFPS ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q110786, wd:Q682111, wd:Q442485 .
  }

  # SPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretSPSForm .
    ?pretSPSForm ontolex:representation ?pretSPS ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786, wd:Q682111, wd:Q442485 .
  }

  # TPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretTPSForm .
    ?pretTPSForm ontolex:representation ?pretTPS ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q110786, wd:Q682111, wd:Q442485 .
  }

  # FPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretFPPForm .
    ?pretFPPForm ontolex:representation ?pretFPP ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786, wd:Q682111, wd:Q442485 .
  }

  # SPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretSPPForm .
    ?pretSPPForm ontolex:representation ?pretSPP ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786, wd:Q682111, wd:Q442485 .
  }

  # TPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretTPPForm .
    ?pretTPPForm ontolex:representation ?pretTPP ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q146786, wd:Q682111, wd:Q442485 .
  }
}
