
# tool: scribe-data
# All Italian (Q652) verbs and the currently implemented tenses for each.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?pretFPS ?pretSPS ?pretTPS
  ?pretFPP ?pretSPP ?pretTPP

WHERE {
  ?lexeme dct:language wd:Q652 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?infinitive .

  # MARK: Preterite

  # FPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretFPSForm .
    ?pretFPSForm ontolex:representation ?pretFPS ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q21714344, wd:Q110786 .
  }

  # SPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretSPSForm .
    ?pretSPSForm ontolex:representation ?pretSPS ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q51929049, wd:Q110786 .
  }

  # TPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretTPSForm .
    ?pretTPSForm ontolex:representation ?pretTPS ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q51929074, wd:Q110786 .
  }

  # FPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretFPPForm .
    ?pretFPPForm ontolex:representation ?pretFPP ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q21714344, wd:Q146786 .
  }

  # SPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretSPPForm .
    ?pretSPPForm ontolex:representation ?pretSPP ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q51929049, wd:Q146786 .
  }

  # TPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretTPPForm .
    ?pretTPPForm ontolex:representation ?pretTPP ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q51929074, wd:Q146786 .
  }
}
