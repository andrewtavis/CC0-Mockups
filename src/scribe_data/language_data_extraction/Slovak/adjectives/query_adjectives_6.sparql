# tool: scribe-data
# All Slovak (Q9058) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?feminineInstrumentalSingular
  ?masculineInstrumentalSingular
  ?neuterInstrumentalSingular
  ?instrumentalPlural

WHERE {
  ?lexeme dct:language wd:Q9058;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .

  # MARK: Instrumental

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineInstrumentalSingularForm .
    ?feminineInstrumentalSingularForm ontolex:representation ?feminineInstrumentalSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q192997, wd:Q110786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineInstrumentalSingularForm .
    ?masculineInstrumentalSingularForm ontolex:representation ?masculineInstrumentalSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q192997, wd:Q110786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterInstrumentalSingularForm .
    ?neuterInstrumentalSingularForm ontolex:representation ?neuterInstrumentalSingular ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q192997, wd:Q110786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?instrumentalPluralForm .
    ?instrumentalPluralForm ontolex:representation ?instrumentalPlural ;
      wikibase:grammaticalFeature wd:Q192997, wd:Q146786, wd:Q3482678 .
  }
}
