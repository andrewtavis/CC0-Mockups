# tool: scribe-data
# All Czech (Q9056) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)

  ?infinitive

  ?presFPS ?presSPS ?presTPS
  ?presFPP ?presSPP ?presTPP

  ?FPPImp ?SPSImp ?SPPImp

  ?femSingularActivePart
  ?masAnimateSingularActivePart
  ?masInanimateSingularActivePart
  ?neutSingularActivePart
  ?femPluralActivePart
  ?masAnimatePluralActivePart
  ?masInanimatePluralActivePart
  ?neutPluralActivePart

WHERE {
  ?lexeme dct:language wd:Q9056 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?infinitive .

  # MARK: Indicative Present

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presFPSForm .
    ?presFPSForm ontolex:representation ?presFPS ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q110786, wd:Q682111, wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSPSForm .
    ?presSPSForm ontolex:representation ?presSPS ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786, wd:Q682111, wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presTPSForm .
    ?presTPSForm ontolex:representation ?presTPS ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q110786, wd:Q682111, wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presFPPForm .
    ?presFPPForm ontolex:representation ?presFPP ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786, wd:Q682111, wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSPPForm .
    ?presSPPForm ontolex:representation ?presSPP ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786, wd:Q682111, wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presTPPForm .
    ?presTPPForm ontolex:representation ?presTPP ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q146786, wd:Q682111, wd:Q192613 .
  }

  # MARK: Imperative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?FPPImpForm .
    ?FPPImpForm ontolex:representation ?FPPImp ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786, wd:Q22716 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?SPSImpForm .
    ?SPSImpForm ontolex:representation ?SPSImp ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786, wd:Q22716 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?SPPImpForm .
    ?SPPImpForm ontolex:representation ?SPPImp ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786, wd:Q22716 .
  }

  # MARK: Active Participle

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femSingularActivePartForm .
    ?femSingularActivePartForm ontolex:representation ?femSingularActivePart ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786, wd:Q72249355 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masAnimateSingularActivePartForm .
    ?masAnimateSingularActivePartForm ontolex:representation ?masAnimateSingularActivePart ;
      wikibase:grammaticalFeature wd:Q54020116, wd:Q110786, wd:Q72249355 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masInanimateSingularActivePartForm .
    ?masInanimateSingularActivePartForm ontolex:representation ?masInanimateSingularActivePart ;
      wikibase:grammaticalFeature wd:Q52943434, wd:Q110786, wd:Q72249355 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neutSingularActivePartForm .
    ?neutSingularActivePartForm ontolex:representation ?neutSingularActivePart ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q110786, wd:Q72249355 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femPluralActivePartForm .
    ?femPluralActivePartForm ontolex:representation ?femPluralActivePart ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786, wd:Q72249355 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masAnimatePluralActivePartForm .
    ?masAnimatePluralActivePartForm ontolex:representation ?masAnimatePluralActivePart ;
      wikibase:grammaticalFeature wd:Q54020116, wd:Q146786, wd:Q72249355 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masInanimatePluralActivePartForm .
    ?masInanimatePluralActivePartForm ontolex:representation ?masInanimatePluralActivePart ;
      wikibase:grammaticalFeature wd:Q52943434, wd:Q146786, wd:Q72249355 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neutPluralActivePartForm .
    ?neutPluralActivePartForm ontolex:representation ?neutPluralActivePart ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q146786, wd:Q72249355 .
  }
}
