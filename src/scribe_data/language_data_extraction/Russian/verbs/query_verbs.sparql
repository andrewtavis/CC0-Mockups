# tool: scribe-data
# All Russian (Q7737) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?presFPS ?presSPS ?presTPS
  ?presFPP ?presSPP ?presTPP
  ?pastFeminine ?pastMasculine ?pastNeutral ?pastPlural

WHERE {
  ?lexeme dct:language wd:Q7737 ;
    wikibase:lexicalCategory wd:Q24905 .

  # MARK: Infinitive

  ?lexeme ontolex:lexicalForm ?infinitiveForm .
  ?infinitiveForm ontolex:representation ?infinitive ;
    wikibase:grammaticalFeature wd:Q179230 .

  # MARK: Present

  # FPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presFPSForm .
    ?presFPSForm ontolex:representation ?presFPS ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q110786, wd:Q682111, wd:Q192613 .
  }

  # SPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSPSForm .
    ?presSPSForm ontolex:representation ?presSPS ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786, wd:Q682111, wd:Q192613 .
  }

  # TPS
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presTPSForm .
    ?presTPSForm ontolex:representation ?presTPS ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q110786, wd:Q682111, wd:Q192613 .
  }

  # FPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presFPPForm .
    ?presFPPForm ontolex:representation ?presFPP ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786, wd:Q682111, wd:Q192613 .
  }

  # SPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSPPForm .
    ?presSPPForm ontolex:representation ?presSPP ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786, wd:Q682111, wd:Q192613 .
  }

  # TPP
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presTPPForm .
    ?presTPPForm ontolex:representation ?presTPP ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q146786, wd:Q682111, wd:Q192613 .
  }

  # MARK: Past Feminine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastFeminineForm .
    ?pastFeminineForm ontolex:representation ?pastFeminine ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q1994301, wd:Q1775415 .
  }

  # MARK: Past Masculine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastMasculineForm .
    ?pastMasculineForm ontolex:representation ?pastMasculine ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q1994301, wd:Q499327 .
  }

  # MARK: Past Neutral

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastNeutralForm .
    ?pastNeutralForm ontolex:representation ?pastNeutral ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q1994301, wd:Q1775461 .
  }

  # MARK: Past Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastPluralForm .
    ?pastPluralForm ontolex:representation ?pastPlural ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q682111, wd:Q1994301 .
  }
}
