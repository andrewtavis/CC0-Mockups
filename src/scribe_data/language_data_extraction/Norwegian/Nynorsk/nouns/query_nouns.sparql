# tool: scribe-data
# All Nynorsk Norwegian (Q25164) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

# Note: This query is for Nynorsk (Q25164) rather than Bokm√•l (Q25167).

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?indefSingular
  ?defSingular
  ?indefPlural
  ?defPlural
  ?gender

WHERE {
  ?lexeme dct:language wd:Q25164 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?indefSingular .

  # MARK: Definite Singular

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?defSingularForm .
    ?defSingularForm ontolex:representation ?defSingular ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q53997851 .
  }

  # MARK: Indefinite Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indefPluralForm .
    ?indefPluralForm ontolex:representation ?indefPlural ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q53997857 .
  }

  # MARK: Definite Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?defPluralForm .
    ?defPluralForm ontolex:representation ?defPlural ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q53997851 .
  }

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
