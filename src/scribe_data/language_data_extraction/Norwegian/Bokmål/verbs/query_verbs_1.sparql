# tool: scribe-data
# All Norwegian Bokmål (Q9043) verbs and the given forms.
# Enter this query at https://query.wikidata.org/.

# Note: This query is for Bokmål (Q25167) rather than Nynorsk (Q25164).

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive ?presentTense ?preterite ?presentPerfect
WHERE {
  ?lexeme dct:language wd:Q25167;
          wikibase:lexicalCategory wd:Q24905;
          wikibase:lemma ?infinitive.

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm.
    ?presentForm ontolex:representation ?presentTense;
      wikibase:grammaticalFeature wd:Q192613.  #
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteForm.
    ?preteriteForm ontolex:representation ?preterite;
      wikibase:grammaticalFeature wd:Q442485.
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentPerfectForm.
    ?presentPerfectForm ontolex:representation ?presentPerfect;
      wikibase:grammaticalFeature wd:Q1240211.
  }
}
