# tool: scribe-data
# All Latin (Q397) verbs.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  (GROUP_CONCAT(DISTINCT ?passiveSingular; SEPARATOR=", ") AS ?passiveSingularForms)

WHERE {
  # Latin verb lexemes
  ?lexeme dct:language wd:Q397 ;
          wikibase:lexicalCategory wd:Q24905 ;  # Verb
          wikibase:lemma ?verb .

  # Passive Singular
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?formPassiveSingular .
    ?formPassiveSingular ontolex:representation ?passiveSingular ;
    wikibase:grammaticalFeature wd:Q110786, wd:Q1194697 .  # Singular, Passive voice
    FILTER(LANG(?passiveSingular) = "la")
  }
}
GROUP BY ?lexeme ?verb
