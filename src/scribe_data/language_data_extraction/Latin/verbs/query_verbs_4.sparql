# tool: scribe-data
# All Latin (Q397) verbs with passive plural.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  (GROUP_CONCAT(DISTINCT ?passivePlural; SEPARATOR=", ") AS ?passivePluralForms)

WHERE {
  # Latin verb lexemes
  ?lexeme dct:language wd:Q397 ;
          wikibase:lexicalCategory wd:Q24905 ;  # Verb
          wikibase:lemma ?verb .

  # Passive Plural
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?formPassivePlural .
    ?formPassivePlural ontolex:representation ?passivePlural ;
    wikibase:grammaticalFeature wd:Q146786, wd:Q1194697 .  # Plural, Passive voice
    FILTER(LANG(?passivePlural) = "la")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],la".
  }
}
GROUP BY ?lexeme ?verb
