# tool: scribe-data
# All Latin (Q397) nouns with nominative and genitive case.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?Noun
  ?nomSingular
  ?nomPlural
  ?genSingular
  ?genPlural
  
WHERE {
  ?lexeme dct:language wd:Q397 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?Noun .

  # MARK: Nominative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nomSingularForm .
    ?nomSingularForm ontolex:representation ?nomSingular ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q110786 .
  } .
  
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nomPluralForm .
    ?nomPluralForm ontolex:representation ?nomPlural ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .
  
  # MARK: Genitive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genSingularForm .
    ?genSingularForm ontolex:representation ?genSingular ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q110786 .
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genPluralForm .
    ?genPluralForm ontolex:representation ?genPlural ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .
}
