SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lemma
  (GROUP_CONCAT(DISTINCT ?directCase; SEPARATOR=", ") AS ?directCases)
  (GROUP_CONCAT(DISTINCT ?singulativeNumeral; SEPARATOR=", ") AS ?singulativeNumerals)
  (GROUP_CONCAT(DISTINCT ?collectiveNumeral; SEPARATOR=", ") AS ?collectiveNumerals)
  (GROUP_CONCAT(DISTINCT ?obliqueCase; SEPARATOR=", ") AS ?obliqueCases)
WHERE {
  # Adjective Lexeme
  ?lexeme dct:language wd:Q11051 ;  # Hindustani language
          wikibase:lexicalCategory wd:Q34698 ;  # Adjective category
          wikibase:lemma ?lemma .
  FILTER(lang(?lemma) = "hi")  # Filter for Hindi language lemmas

  # MARK: Direct Case
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directCaseForm .
    ?directCaseForm ontolex:representation ?directCase ;
    wikibase:grammaticalFeature wd:Q1751855 .  # Direct case
    FILTER(LANG(?directCase) = "hi")
  }

  # MARK: Singulative Numeral
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singulativeNumeralForm .
    ?singulativeNumeralForm ontolex:representation ?singulativeNumeral ;
    wikibase:grammaticalFeature wd:Q110786 .  # Singulative numeral
    FILTER(LANG(?singulativeNumeral) = "hi")
  }

  # MARK: Collective Numeral
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?collectiveNumeralForm .
    ?collectiveNumeralForm ontolex:representation ?collectiveNumeral ;
    wikibase:grammaticalFeature wd:Q146786 .  # Collective numeral
    FILTER(LANG(?collectiveNumeral) = "hi")
  }

  # MARK: Oblique Case
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?obliqueCaseForm .
    ?obliqueCaseForm ontolex:representation ?obliqueCase ;
    wikibase:grammaticalFeature wd:Q1233197 .  # Oblique case
    FILTER(LANG(?obliqueCase) = "hi")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
  }
}
GROUP BY ?lexeme ?lemma
