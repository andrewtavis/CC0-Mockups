# tool: scribe-data
# All Hindi (from Hindustani Q11051) verbs and the currently implemented forms for each.
# Enter this query at https://query.wikidata.org/.
# Note the necessity to filter for "hi" to remove Urdu (ur) words.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?directCase
  ?gerund
  ?intransitivePhase
  ?basicPhase

WHERE {
  # MARK: Infinitive

  ?lexeme dct:language wd:Q11051 ;
          wikibase:lexicalCategory wd:Q24905 ;
          wikibase:lemma ?infinitive .
  FILTER(lang(?infinitive) = "hi") .

  # MARK: Direct Case

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directCaseForm .
    ?directCaseForm ontolex:representation ?directCase ;
    wikibase:grammaticalFeature wd:Q1751855 .
    FILTER(LANG(?directCase) = "hi") .
  } .

  # MARK: Gerund

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?gerundForm .
    ?gerundForm ontolex:representation ?gerund ;
    wikibase:grammaticalFeature wd:Q1923028 .
    FILTER(LANG(?gerund) = "hi") .
  } .

  # MARK: Intransitive Phase

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?intransitivePhaseForm .
    ?intransitivePhaseForm ontolex:representation ?intransitivePhase ;
    wikibase:grammaticalFeature wd:Q113330736 .
    FILTER(LANG(?intransitivePhase) = "hi") .
  } .

  # MARK: Basic Phase

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?basicPhaseForm .
    ?basicPhaseForm ontolex:representation ?basicPhase ;
    wikibase:grammaticalFeature wd:Q113330960 .
    FILTER(LANG(?basicPhase) = "hi") .
  } .
}
