# tool: scribe-data
# All Hebrew (Q9288) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?femininePresentSingular
  ?masculinePresentSingular
  ?femininePresentPlural
  ?masculinePresentPlural

WHERE {
  ?lexeme dct:language wd:Q9288 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?infinitive .
    FILTER(lang(?infinitive) = "he")

  # MARK: Present

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePresentSingularForm .
    ?femininePresentSingularForm ontolex:representation ?femininePresentSingular ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q192613, wd:Q1775415 .
      FILTER(lang(?femininePresentSingular) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePresentSingularForm .
    ?masculinePresentSingularForm ontolex:representation ?masculinePresentSingular ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q192613, wd:Q499327 .
      FILTER(lang(?masculinePresentSingular) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePresentPluralForm .
    ?femininePresentPluralForm ontolex:representation ?femininePresentPlural ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q192613, wd:Q1775415 .
      FILTER(lang(?femininePresentPlural) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePresentPluralForm .
    ?masculinePresentPluralForm ontolex:representation ?masculinePresentPlural ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q192613, wd:Q499327 .
      FILTER(lang(?masculinePresentPlural) = "he")
  }
}
