# tool: scribe-data
# All Bokmål Norwegian (Q9043) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

# Note: This query is for Bokmål (Q25167) rather than Nynorsk (Q25164).

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?indefiniteSingular
  ?definiteSingular
  ?indefinitePlural
  ?definitePlural
  ?gender

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?indefiniteSingular .

  # MARK: Definite Singular

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteSingularForm .
    ?definiteSingularForm ontolex:representation ?definiteSingular ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q53997851 .
  }

  # MARK: Indefinite Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indefinitePluralForm .
    ?indefinitePluralForm ontolex:representation ?indefinitePlural ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q53997857 .
  }

  # MARK: Definite Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definitePluralForm .
    ?definitePluralForm ontolex:representation ?definitePlural ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q53997851 .
  }

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
